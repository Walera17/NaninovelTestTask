@hideChars
@back Loc1.Dissolve time:1 wait:false
@stopBgm fade:1 wait:false
@bgm Locat1 fade:1 wait:false loop:true
@char ElenNovel look:Right pos:25,1
@wait 0.5
@char Sophie look:Left pos:75

@goto .RewardReceived if:RewardExist("Sword")

@wait 1.5
@char ElenNovel.Greeting
ElenNovel: Hello. My name is {name} and I am pleased to meet you.

@printer Bubble.Right pos:52,68
@char Sophie.Greeting
Sophie: My name is Sophie and I am pleased to meet you. I need a sword. I will pay well. Will you get?

@wait 0.5
@printer Dialogue
@char ElenNovel.Thinking
@wait 2.5
ElenNovel: Good. I will take out a sword and bring it to you. 
@showLog name:{T_Message2}

@wait 1.0
@hidePrinter
@goto Location2

# FinishDialog
@back Loc1 time:1 wait:false
@stopBgm fade:1 wait:false
@bgm Locat1 fade:1 wait:false loop:true
@char ElenNovel look:Right pos:25,1
@wait 0.5
@char Jody look:Left pos:50
@wait 0.5
@char Sophie look:Left pos:75

@wait 1.5
@char ElenNovel.Greeting
ElenNovel: Hello. I have been looking for you for a long time. I managed to get a sword.
@showLog name:{T_Message8}
@char Sophie.Greeting
Sophie: Hello {name} - I want to get a sword that I ordered. I will pay well.
@char Jody.Greeting
Jody: Hello {name} - I also want to get a sword. I will pay well.
@hidePrinter
@showLog name:{""}

# Choice
;@wait 3.2
@showUI time:1
@choice "Leave objects to yourself" goto:.Self
@choice "Give only Sophie" goto:.OnlySophie
@choice "Give only Jody" goto:.OnlyJody
@choice "Exit from the choice block" goto:.ExitChoice

@stop

# Self
@hideUI time:1
@wait 0.6
@set rewardJody=false
@set rewardSophie=false
@char Sophie.Insult
@char Jody.Taunt
@wait 3
@goto .Choice

# OnlySophie
@hideUI time:1
@wait 0.6
@set rewardJody=false
@set rewardSophie=true
@char Sophie.Thankful
@char Jody.Whatever
@wait 3
@goto .Choice

# OnlyJody
@hideUI time:1
@wait 0.6
@set rewardJody=true
@set rewardSophie=false
@char Sophie.Whatever
@char Jody.Thankful
@wait 3
@goto .Choice

# ExitChoice
@showLog name:{"EnableGlobalMap"}
@hide Jody
@stop

# RewardReceived
@if rewardSophie
@char Sophie..Greeting
Sophie: Hello {name} - You are a good person. It's nice to do business with you.[wait 1.2][skipInput]
@else
@char Sophie.Dismissing
Sophie: {name} - You are a scoundrel. I do not want to have any business with you.[wait 5][skipInput]
@endIf
@showLog name:{"EnableGlobalMap"}
@stop
